import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'java-library'
    id 'java'
    id "com.vanniktech.maven.publish" version "0.28.0"
    id 'signing'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'checkstyle'
}

allprojects {
    group = "io.github.yonggoose"
    version = "0.7.5"
}

subprojects {
    apply {
        plugin 'checkstyle'
    }

    checkstyle {
        maxWarnings = 0
        toolVersion = '9.3'
        configFile = file("${rootDir}/tool/naver-checkstyle-rules.xml")
        configProperties = ["suppressionFile": "${rootDir}/tool/naver-checkstyle-suppressions.xml"]

    }

    build {
        dependsOn 'checkstyleMain'
        dependsOn 'checkstyleTest'
    }
}

repositories {
    mavenCentral()
}

javadoc {
    exclude '**/maker-tests/**'
}

signing {
    useGpgCmd()
    sign publishing.publications
}

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Empty(), true))

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    coordinates(project.group.toString(), "MaKer", "0.7.5")

    pom {
        name = "MaKer"
        description = "MaKer is a comprehensive Java library designed to enhance " +
                "the functionality and efficiency of your applications."
        inceptionYear = "2024"
        url = "https://github.com/IDMaker-io/MaKer"
        licenses {
            license {
                name = "The Eclipse Public License, Version 2.0"
                url = "https://github.com/IDMaker-io/MaKer/blob/main/LICENSE"
            }
        }
        developers {
            developer {
                id = "YongGoose"
                name = "Yongjun Hong"
                url = "https://github.com/YongGoose"
            }
        }

        scm {
            connection = "scm:git:git://github.com/IDMaker-io/MaKer.git"
            developerConnection = "scm:git:ssh://github.com:IDMaker-io/MaKer.git"
            url = "https://github.com/IDMaker-io/MaKer/tree/main"
        }
    }
}

dependencies {
    api project(':id-maker')
    api project(':password-maker')
    api project(':event-date-time-maker')
}

tasks.register('aggregateJavadocs', Javadoc) {
    group = 'Documentation'
    description = 'Aggregates Javadoc API documentation of all subprojects.'

    def excludedModules = ['maker-tests']

    source subprojects.findAll { project ->
        !excludedModules.contains(project.name)
    }.collect { project ->
        project.sourceSets.main.allJava
    }

    classpath = files(subprojects.findAll { project ->
        !excludedModules.contains(project.name)
    }.collect { project ->
        project.sourceSets.main.compileClasspath
    })

    subprojects.findAll { project ->
        !excludedModules.contains(project.name)
    }.each { subproject ->
        subproject.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }

    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        author = true
        version = true
        use = true
        windowTitle = "MaKer API Documentation"
        docTitle = "MaKer API Documentation"
        header = "MaKer"
    }

    destinationDir = file("${rootProject.DEFAULT_BUILD_DIR_NAME}/docs/javadoc")
    failOnError = false
}

tasks.named('bootJar') {
    enabled = false
}
